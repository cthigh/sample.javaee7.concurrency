apply plugin: 'war'
apply plugin: 'liberty'
apply plugin: 'maven'

group = 'net.wasdev.wlp.sample.javaee7'
version = '1.0-SNAPSHOT'

description = "Java EE 7 Sample Concurrency"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0-SNAPSHOT'
    }
}

configurations{
     derby { transitive = false }
}

repositories {
     mavenCentral()
}
dependencies {
    providedCompile group: 'org.apache.derby', name: 'derby', version:'10.13.1.1'
    providedCompile group: 'javax.json', name: 'javax.json-api', version:'1.0'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    providedCompile group: 'org.eclipse.persistence', name: 'javax.persistence', version:'2.1.0'
    providedCompile group: 'javax.enterprise.concurrent', name: 'javax.enterprise.concurrent-api', version:'1.0'
    providedCompile group: 'javax.transaction', name: 'javax.transaction-api', version:'1.2'
    providedCompile group: 'javax.enterprise', name: 'cdi-api', version:'1.2'
    providedCompile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version:'2.0'

    derby group: 'org.apache.derby', name: 'derby', version: '10.13.1.1'
    libertyRuntime  group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-javaee7', version: '17.0.0.2'
}

ext {
    testServerHttpPort = 9080
    testServerHttpsPort = 9443
    warContext = 'JDBCApp'
    packagingType = 'usr'
}

war {
    archiveName = baseName + '.' + extension
}

liberty {
    server {
        configFile = file('src/main/wlp/server.xml')
        bootstrapProperties = ['appLocation': war.archiveName]
        looseApplication = false
    }
}

task copyDerby {
    doLast {
        copy {
            from configurations.derby
            into "$buildDir/wlp/usr/shared/resources/derby"
            include '*.jar'
            rename "derby-10.13.1.1.jar", 'derby.jar'
        }
    }
}

test {
    println 'inside the unit test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.context': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

clean.dependsOn 'libertyStop'
check.dependsOn 'integrationTest'
installApps.dependsOn 'war'
libertyStart.dependsOn 'copyDerby', 'installApps'
copyDerby.dependsOn 'libertyCreate'
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
